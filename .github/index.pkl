//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//
amends "@pkl.impl.ghactions/PklCI.pkl"

import "@com.github.actions/catalog.pkl"
import "@com.github.actions/context.pkl"
import "@com.github.actions/Workflow.pkl"

build = baseWorkflow

prb = baseWorkflow

main {
  on {
    workflow_dispatch {
      inputs {
        ["source_run"] {
          type = "string"
          required = false
          description = "The source GitHub Action workflow run triggering this build"
        }
      }
    }
  }
  jobs {
    ["build-and-publish-docs"] = publishJob
  }
}

triggerDocsBuild = "both"

local baseWorkflow: Workflow = new {
  jobs {
    ["build-and-test"] = testJob
  }
}

local testJob: Workflow.Job = new {
  name = "Build and test"
  `runs-on` = "ubuntu-latest"
  steps {
    (catalog.`actions/checkout@v6`) {
      with {
        `fetch-depth` = 0
      }
    }
    (catalog.`actions/setup-java@v5`) {
      name = "Setup Java"
      with {
        distribution = "temurin"
        `java-version` = "21"
        cache = "gradle"
      }
    }
    new {
      name = "Build and test"
      run = "./gradlew check"
    }
  }
}

local publishJob: Workflow.Job = new {
  name = "Generate and publish docs"
  `runs-on` = "ubuntu-latest"
  permissions {
    contents = "write"
  }
  steps {
    new {
      name = "Triggered by"
      `if` = "inputs.source_run != null"
      run =
        """
        echo "Triggered by workflow in repo: ${INPUTS_SOURCE_RUN}" >> $GITHUB_STEP_SUMMARY
        """
      env {
        ["INPUTS_SOURCE_RUN"] = "${{ inputs.source_run }}"
      }
    }
    (catalog.`actions/checkout@v6`) {
      with {
        `fetch-depth` = 0
      }
    }
    (catalog.`actions/setup-java@v5`) {
      name = "Setup Java"
      with {
        distribution = "temurin"
        `java-version` = "21"
        cache = "gradle"
      }
    }
    // check out again to write credentials to workspace (needed by publish task).
    (catalog.`actions/checkout@v6`) {
      with {
        `fetch-depth` = 0
        `persist-credentials` = true
      }
    }
    new {
      env {
        ["GH_TOKEN"] = context.github.token
      }
      name = "Generate and publish docs"
      run = "./gradlew check generateAndPublishDocs --refresh-dependencies"
    }
  }
}
